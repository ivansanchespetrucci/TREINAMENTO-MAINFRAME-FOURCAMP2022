      *===============================================================*
       IDENTIFICATION                                        DIVISION.
      *===============================================================*
       PROGRAM-ID. F19CIP1.
      *===============================================================*
      *   AUTOR....:RAFAEL VIANA                                      *
      *   ANALISTA.:IVAN PETRUCCI                  - INSTRUTOR        *
      *   DATA ....:20/06/2022                                        *
      *---------------------------------------------------------------*
      *   OBJETIVO:PROGRAMA PARA CONSULTAR, INSERIR E DELETAR         *
      *    REGISTROS EM TABELA DB2, EXECUTANDO NO CICS.               *
      *                                                               *
      *                                                               *
      *---------------------------------------------------------------*
      *   ARQUIVOS:                                                   *
      *   DDNAME......                                                *
      *    DDNAME              I/O                 INCLUDE/BOOK       *
      *---------------------------------------------------------------*
      *   MODULOS....:                             INCLUDE/BOOK       *
      *===============================================================*

      *===============================================================*
       ENVIRONMENT                                            DIVISION.
      *===============================================================*
       CONFIGURATION                                           SECTION.
       SPECIAL-NAMES.
           DECIMAL-POINT IS COMMA.

      *===============================================================*
       DATA                                                   DIVISION.
      *===============================================================*
       WORKING-STORAGE SECTION.

           EXEC SQL
              INCLUDE #BKFUNC
           END-EXEC.

           EXEC SQL
              INCLUDE SQLCA
           END-EXEC.

           COPY F19CIM2.
           COPY DFHAID.


       01 FILLER PIC X(50) VALUE
             '------VARIAVEL DE RECEBIMENTO DE DATA------'.

       01 WRK-DATAADM.
          05 WRK-DATAANO    PIC X(04).
          05 FILLER         PIC X(01) VALUE '-'.
          05 WRK-DATAMES    PIC X(02).
          05 FILLER         PIC X(01) VALUE '-'.
          05 WRK-DATADIA    PIC X(02).

       01 FILLER PIC X(50) VALUE
             '------MASCARA------------------------------'.

       77 WRK-SALARIO-ED    PIC ZZ.ZZZ.ZZZ,ZZ.

       01 FILLER PIC X(50) VALUE
             '------VARIAVEL DE APOIO E STATUS-----------'.

       77 WRK-POS            PIC 9(02).
       77 WRK-EMAIL-NULL     PIC S9(04) COMP.
       77 WRK-SQLCODE        PIC -999.
       77 WRK-RESP           PIC S9(04) COMP.
       77 WRK-RET-MAPA       PIC S9(04) COMP.
       77 WRK-FLAG           PIC 9(01).
       77 WRK-SAIDA          PIC X(30) VALUE '---- OBRIGADO!----'.


      *===============================================================*
       PROCEDURE                                 DIVISION.
      *===============================================================*
      *---------------------------------------------------------------*
       0000-PRINCIPAL                                         SECTION.
      *---------------------------------------------------------------*

             PERFORM 1000-INICIALIZAR.
             IF WRK-FLAG EQUAL 1
              PERFORM 2000-PROCESSAR
             END-IF.
             PERFORM 3000-FINALIZAR.

               EXEC CICS
                 RETURN TRANSID ('T191')
               END-EXEC.

       0000-99-FIM.                        EXIT.

      *---------------------------------------------------------------*
       1000-INICIALIZAR                                       SECTION.
      *---------------------------------------------------------------*

                EXEC CICS RECEIVE
                  MAPSET('F19CIM2')
                  MAP('MAPA01')
                  INTO(MAPA01I)
                  RESP(WRK-RET-MAPA)
                END-EXEC.

               IF WRK-RET-MAPA EQUAL DFHRESP(MAPFAIL)
                MOVE 2 TO WRK-FLAG
               ELSE
                MOVE 1 TO WRK-FLAG
               END-IF.


       1000-99-FIM.                        EXIT.

      *---------------------------------------------------------------*
       2000-PROCESSAR                                         SECTION.
      *---------------------------------------------------------------*

      *-----------RECEBE DADOS ---------------------------------------*


             EVALUATE EIBAID
             WHEN ''''
              INITIALIZE MAPA01O
              INITIALIZE MAPA01I
              MOVE ZEROS TO IDO
             WHEN ':'
               EXEC CICS SEND
                 FROM(WRK-SAIDA)
                 ERASE
               END-EXEC

               EXEC CICS
                 RETURN
               END-EXEC

      *-------------CONSULTA---------------------------------------
             WHEN '5'
                MOVE IDI TO DB2-ID
                 EXEC SQL
                   SELECT ID,NOME,SETOR,SALARIO,DATAADM,EMAIL
                   INTO  :DB2-ID,
                         :DB2-NOME,
                         :DB2-SETOR,
                         :DB2-SALARIO,
                         :DB2-DATAADM,
                         :DB2-EMAIL  :WRK-EMAIL-NULL
                   FROM IVAN.FUNC
                    WHERE ID =:DB2-ID
                    FETCH FIRST ROW ONLY
                 END-EXEC
              PERFORM 2200-VERIFICACAO-CONSULTA
      *-------------INSERIR----------------------------------------
             WHEN '6'
              PERFORM 2300-VALIDACAO-DOS-DADOS

               IF WRK-POS EQUAL 7
                MOVE IDI        TO DB2-ID
                MOVE NOMEI      TO DB2-NOME
                MOVE SETORI     TO DB2-SETOR
                MOVE SALARIOI   TO DB2-SALARIO
      *----------MOVIMENTACAO DA DATA---------------------------------*
                 MOVE DATADIAI       TO WRK-DATADIA
                 MOVE DATAMESI       TO WRK-DATAMES
                 MOVE DATAANOI       TO WRK-DATAANO
      *---------------------------------------------------------------*
                 MOVE WRK-DATAADM    TO DB2-DATAADM
                MOVE EMAILI     TO DB2-EMAIL

                EXEC SQL
                  INSERT INTO IVAN.FUNC(ID,NOME,SETOR,SALARIO,DATAADM,
                                      EMAIL)
                  VALUES (:DB2-ID,
                          :DB2-NOME,
                          :DB2-SETOR,
                          :DB2-SALARIO,
                          :DB2-DATAADM,
                          :DB2-EMAIL)
                END-EXEC

               PERFORM 2100-VERIFICACAO-INSERIR
              END-IF
      *---------------------- EXCLUSAO--------------------------------*
              WHEN '7'
                 MOVE IDI TO DB2-ID

                 EXEC SQL
                     DELETE FROM IVAN.FUNC WHERE ID = :DB2-ID
                 END-EXEC
                PERFORM 2400-VERIFICACAO-EXCLUSAO
             END-EVALUATE.
       2000-99-FIM.                        EXIT.
      *---------------------------------------------------------------*
       2100-VERIFICACAO-INSERIR                               SECTION.
      *---------------------------------------------------------------*
              EVALUATE SQLCODE
                WHEN 0
                  EXEC SQL
                    COMMIT
                  END-EXEC
                  MOVE 'REGISTRO INSERIDO        ' TO MSGO
                WHEN -180
                  MOVE 'FORMATO DA DATA INVALIDO ' TO MSGO
                WHEN -181
                  MOVE 'VERIFIQUE A DATA         ' TO MSGO
                WHEN OTHER
                  MOVE SQLCODE TO WRK-SQLCODE
                  MOVE WRK-SQLCODE TO MSGO
              END-EVALUATE.
       2100-99-FIM.                        EXIT.
      *---------------------------------------------------------------*
       2200-VERIFICACAO-CONSULTA                              SECTION.
      *---------------------------------------------------------------*
              EVALUATE SQLCODE
                WHEN 0
                 MOVE DB2-NOME       TO NOMEO
                 MOVE DB2-SETOR      TO SETORO
                 MOVE DB2-SALARIO    TO WRK-SALARIO-ED
                 MOVE WRK-SALARIO-ED TO SALARIOO
                 MOVE DB2-DATAADM    TO WRK-DATAADM
                 MOVE WRK-DATADIA    TO DATADIAO
                 MOVE WRK-DATAMES    TO DATAMESO
                 MOVE WRK-DATAANO    TO DATAANOO
                IF WRK-EMAIL-NULL    EQUAL 0
                 MOVE DB2-EMAIL      TO EMAILO
                END-IF
                 MOVE 'REGISTRO ENCONTRADO     ' TO MSGO
                WHEN 100
                 INITIALIZE MAPA01O
                 INITIALIZE SALARIOI
                 MOVE DB2-ID TO IDO

                 MOVE 'REGISTRO NAO ENCONTRADO ' TO MSGO
                WHEN OTHER
                 MOVE SQLCODE TO WRK-SQLCODE
                 MOVE 'ERRO.:'                   TO MSGO
                 MOVE WRK-SQLCODE                TO MSGO(08:04)
              END-EVALUATE.
       2200-99-FIM.                        EXIT.
      *---------------------------------------------------------------*
       2300-VALIDACAO-DOS-DADOS                              SECTION.
      *---------------------------------------------------------------*
              MOVE 'ERRO: ' TO MSGO.
               MOVE 7 TO WRK-POS.

               IF IDL EQUAL 0 OR IDI  EQUAL ZEROS
                  MOVE 'ID,' TO MSGO(WRK-POS:3)
                  ADD 3 TO WRK-POS
               END-IF.

               IF NOMEL EQUAL 0 OR NOMEI EQUAL SPACES
                  MOVE 'NOME,' TO MSGO(WRK-POS:5)
                  ADD 5 TO WRK-POS
               END-IF.

               IF SETORL EQUAL 0 OR SETORI EQUAL SPACES
                  MOVE 'SETOR,' TO MSGO(WRK-POS:6)
                  ADD 6 TO WRK-POS
               END-IF.

               IF SALARIOL EQUAL 0 OR SALARIOI EQUAL ZEROS
                      OR SALARIOI IS NOT NUMERIC
                  MOVE 'SALARIO,' TO MSGO(WRK-POS:8)
                  ADD 8 TO WRK-POS
               END-IF.

               IF ( DATAANOL EQUAL 0 OR DATAMESL EQUAL 0
                           OR DATADIAL EQUAL 0 ) OR
                  ( DATAANOI EQUAL SPACES OR DATAMESI EQUAL SPACES
                    OR DATADIAI EQUAL SPACES )
                  MOVE 'DATAADM,' TO MSGO(WRK-POS:8)
                  ADD 8 TO WRK-POS
               END-IF.
       2300-99-FIM.                        EXIT.
      *---------------------------------------------------------------*
       2400-VERIFICACAO-EXCLUSAO                             SECTION.
      *---------------------------------------------------------------*
                 EVALUATE SQLCODE
                  WHEN 0
                   EXEC SQL
                     COMMIT
                   END-EXEC
                     MOVE 'REGISTRO DELETADO       ' TO MSGO
                  WHEN 100
                     MOVE 'REGISTRO NAO ENCONTRADO ' TO MSGO
                  WHEN OTHER
                     MOVE SQLCODE TO WRK-SQLCODE
                     MOVE 'ERRO.:'                   TO MSGO
                     MOVE WRK-SQLCODE                TO MSGO(08:04)
                 END-EVALUATE.
       2400-99-FIM.                        EXIT.
      *---------------------------------------------------------------*
       3000-FINALIZAR                                        SECTION.
      *---------------------------------------------------------------*
      *------------FIM DO PROGRAMA------------------------------------*
           IF WRK-FLAG EQUAL 2
            EXEC CICS SEND
               MAPSET('F19CIM2')
               MAP('MAPA01')
                ERASE
               MAPONLY
            END-EXEC
           ELSE
              EXEC CICS SEND
                 MAPSET('F19CIM2')
                 MAP('MAPA01')
                 DATAONLY
                 FROM( MAPA01O )
              END-EXEC
           END-IF.
       3000-99-FIM.                        EXIT.
