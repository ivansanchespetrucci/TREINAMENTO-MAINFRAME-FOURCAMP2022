      *===============================================================*
       IDENTIFICATION                                        DIVISION.
      *===============================================================*
       PROGRAM-ID. F19LVSA.
      *===============================================================*
      *   AUTOR....:RAFAEL VIANA                                      *
      *   ANALISTA.:IVAN PETRUCCI                  - INSTRUTOR        *
      *   DATA ....:27/06/2022                                        *
      *---------------------------------------------------------------*
      *   OBJETIVO:LEITURA DE UM ARQUIVO (VSAM), E SELECIONANDO PELA  *
      *            SYSOUT UM REGISTRO PARA EXIBIR.                    *
      *                                                               *
      *****************************************************************
      * NA SYSIN DIGITE O NUMERO DA CHAVE DE 5 DIGITOS PARA CONSULTA! *
      *****************************************************************
      *---------------------------------------------------------------*
      *   ARQUIVOS:                                                   *
      *   DDNAME......                                                *
      *    DDNAME              I/O                 INCLUDE/BOOK       *
      *  GRAVAERROS             I                   #GLOG             *
      *   ARQVSAM               I                   #ARQVSA           *
      *---------------------------------------------------------------*
      *   MODULOS....:                             INCLUDE/BOOK       *
      *===============================================================*

      *===============================================================*
       ENVIRONMENT                                            DIVISION.
      *===============================================================*
      *===============================================================*
       CONFIGURATION                                           SECTION.
      *===============================================================*
       SPECIAL-NAMES.
           DECIMAL-POINT IS COMMA.
      *---------------------------------------------------------------*
       INPUT-OUTPUT                                            SECTION.
       FILE-CONTROL.
            SELECT ARQVSAM ASSIGN TO ARQVSAM
                ORGANIZATION IS INDEXED
                ACCESS MODE  IS DYNAMIC
                RECORD KEY   IS ARQ-CHAVE
                FILE STATUS  IS WRK-FS-ARQVSAM.

      *===============================================================*
       DATA                                                   DIVISION.
      *===============================================================*
       FILE                                                    SECTION.
       FD ARQVSAM.

       COPY '#ARQVSA'.

      *===============================================================*
       WORKING-STORAGE                                         SECTION.
      *===============================================================*

       01 FILLER         PIC X(50)
                VALUE '---- BOOK DE VARIAVEIS DO TRATAERROS----'.

       COPY '#GLOG'.

      *---------------------------------------------------------------*
       01 FILLER         PIC X(50)
                VALUE '---- VARIAVEL DE STATUS----'.

       77 WRK-FS-ARQVSAM     PIC 9(02).

      *---------------------------------------------------------------*

       01 FILLER         PIC X(50)
                VALUE '---- MENSAGENS DE ERROS----'.

       77 WRK-MSG-PROGRAMA        PIC X(08) VALUE 'FR19LVSA'.
       77 WRK-MSG-SECAO1          PIC X(04) VALUE '1000'.
       77 WRK-MSG-SECAO2          PIC X(04) VALUE '2000'.
       77 WRK-MSG-SECAO3          PIC X(04) VALUE '3000'.
       77 WRK-MSG-REGENCON        PIC X(26)
                          VALUE 'NENHUM REGISTRO ENCONTRADO'.
       77 WRK-MSG-ARQENCON        PIC X(22)
                          VALUE 'ARQUIVO NAO ENCONTRADO'.
       77 WRK-MSG-ERROOPEN        PIC X(24)
                          VALUE 'ERRO ABERTURA DO ARQUIVO'.
       77 WRK-MSG-ERROREAD        PIC X(23)
                          VALUE 'ERRO LEITURA DO ARQUIVO'.
       77 WRK-MSG-ERROCLOSE       PIC X(21)
                          VALUE 'ERRO CLOSE DO ARQUIVO'.
      *---------------------------------------------------------------*

       01 FILLER         PIC X(50)
                VALUE '---- MENSAGENS DE DISPLAY----'.

       77 WRK-MSG-CHAVE           PIC X(10) VALUE 'CHAVE....:'.
       77 WRK-MSG-NOME            PIC X(10) VALUE 'NOME.....:'.
       77 WRK-MSG-SETOR           PIC X(10) VALUE 'SETOR....:'.
       77 WRK-MSG-SALARIO         PIC X(10) VALUE 'SALARIO..:'.
       77 WRK-MSG-DATAADM         PIC X(10) VALUE 'DATAADM..:'.
       77 WRK-MSG-EMAIL           PIC X(10) VALUE 'EMAIL....:'.
      *---------------------------------------------------------------*

       01 FILLER         PIC X(50)
                VALUE '---- MENSAGENS TRATA ERROS----'.

       77 WRK-MSG-ERROPRINC       PIC X(26)
                          VALUE '*****ERRO NO PROGRAMA*****'.
       77 WRK-MSG-ERROPROGR       PIC X(19)
                          VALUE 'NOME DO PROGRAMA..:'.
       77 WRK-MSG-ERROSECAO       PIC X(19)
                          VALUE 'SECAO ............:'.
       77 WRK-MSG-ERROMSG         PIC X(19)
                          VALUE 'MENSAGEM DO ERRO..:'.
       77 WRK-MSG-ERROSTATUS      PIC X(19)
                          VALUE 'STATUS DO ERRO....:'.
      *---------------------------------------------------------------*

      *===============================================================*
       PROCEDURE DIVISION.
      *===============================================================*

      *---------------------------------------------------------------*
       0000-PRINCIPAL                                          SECTION.
      *---------------------------------------------------------------*

            PERFORM 1000-INICIAR.
            PERFORM 2000-PROCESSAR.
            PERFORM 3000-FINALIZAR.
            STOP RUN.

      *---------------------------------------------------------------*
       1000-INICIAR                                            SECTION.
      *---------------------------------------------------------------*
             OPEN INPUT ARQVSAM.
             PERFORM 1200-TESTAR-OPEN.

       1000-99-FIM.                      EXIT.

      *---------------------------------------------------------------*
       1200-TESTAR-OPEN                                        SECTION.
      *---------------------------------------------------------------*
            EVALUATE WRK-FS-ARQVSAM
             WHEN 0
               GO TO 1200-99-FIM
             WHEN 23
               MOVE WRK-MSG-PROGRAMA             TO WRK-PROGRAMA
               MOVE WRK-MSG-SECAO1               TO WRK-SECAO
               MOVE WRK-MSG-REGENCON             TO WRK-MSGERRO
               MOVE WRK-FS-ARQVSAM               TO WRK-STATUS
             WHEN 35
               MOVE WRK-MSG-PROGRAMA             TO WRK-PROGRAMA
               MOVE WRK-MSG-SECAO1               TO WRK-SECAO
               MOVE WRK-MSG-ARQENCON             TO WRK-MSGERRO
               MOVE WRK-FS-ARQVSAM               TO WRK-STATUS
             WHEN OTHER
               MOVE WRK-MSG-PROGRAMA             TO WRK-PROGRAMA
               MOVE WRK-MSG-SECAO1               TO WRK-SECAO
               MOVE WRK-MSG-ERROOPEN             TO WRK-MSGERRO
               MOVE WRK-FS-ARQVSAM               TO WRK-STATUS
            END-EVALUATE.
               PERFORM 9000-TRATARERROS.

       1200-99-FIM.                      EXIT.

      *---------------------------------------------------------------*
       1300-TESTAR-LEITURA                                     SECTION.
      *---------------------------------------------------------------*
            EVALUATE WRK-FS-ARQVSAM
             WHEN 0
               GO TO 1300-99-FIM
             WHEN 23
               MOVE WRK-MSG-PROGRAMA             TO WRK-PROGRAMA
               MOVE WRK-MSG-SECAO2               TO WRK-SECAO
               MOVE WRK-MSG-REGENCON             TO WRK-MSGERRO
               MOVE WRK-FS-ARQVSAM               TO WRK-STATUS
             WHEN 35
               MOVE WRK-MSG-PROGRAMA             TO WRK-PROGRAMA
               MOVE WRK-MSG-SECAO2               TO WRK-SECAO
               MOVE WRK-MSG-ARQENCON             TO WRK-MSGERRO
               MOVE WRK-FS-ARQVSAM               TO WRK-STATUS
             WHEN OTHER
               MOVE WRK-MSG-PROGRAMA             TO WRK-PROGRAMA
               MOVE WRK-MSG-SECAO2               TO WRK-SECAO
               MOVE WRK-MSG-ERROREAD             TO WRK-MSGERRO
               MOVE WRK-FS-ARQVSAM               TO WRK-STATUS
            END-EVALUATE.
               PERFORM 9000-TRATARERROS.

       1300-99-FIM.                      EXIT.
      *---------------------------------------------------------------*
       1400-TESTAR-CLOSE                                       SECTION.
      *---------------------------------------------------------------*
            EVALUATE WRK-FS-ARQVSAM
             WHEN 0
               GO TO 1400-99-FIM
             WHEN 23
               MOVE WRK-MSG-PROGRAMA             TO WRK-PROGRAMA
               MOVE WRK-MSG-SECAO3               TO WRK-SECAO
               MOVE WRK-MSG-REGENCON             TO WRK-MSGERRO
               MOVE WRK-FS-ARQVSAM               TO WRK-STATUS
             WHEN 35
               MOVE WRK-MSG-PROGRAMA             TO WRK-PROGRAMA
               MOVE WRK-MSG-SECAO3               TO WRK-SECAO
               MOVE WRK-MSG-ARQENCON             TO WRK-MSGERRO
               MOVE WRK-FS-ARQVSAM               TO WRK-STATUS
             WHEN OTHER
               MOVE WRK-MSG-PROGRAMA             TO WRK-PROGRAMA
               MOVE WRK-MSG-SECAO3               TO WRK-SECAO
               MOVE WRK-MSG-ERROCLOSE            TO WRK-MSGERRO
               MOVE WRK-FS-ARQVSAM               TO WRK-STATUS
            END-EVALUATE.
               PERFORM 9000-TRATARERROS.

       1400-99-FIM.                      EXIT.
      *---------------------------------------------------------------*
       2000-PROCESSAR                                          SECTION.
      *---------------------------------------------------------------*
             ACCEPT ARQ-CHAVE FROM SYSIN.
             READ ARQVSAM.
              IF WRK-FS-ARQVSAM NOT EQUAL ZEROS
               PERFORM 1300-TESTAR-LEITURA
              ELSE
               DISPLAY WRK-MSG-CHAVE   ARQ-CHAVE
               DISPLAY WRK-MSG-NOME    ARQ-NOME
               DISPLAY WRK-MSG-SETOR   ARQ-SETOR
               DISPLAY WRK-MSG-SALARIO ARQ-SALARIO
               DISPLAY WRK-MSG-DATAADM ARQ-DATAADM
               DISPLAY WRK-MSG-EMAIL   ARQ-EMAIL
              END-IF.
       2000-99-FIM.                      EXIT.

      *---------------------------------------------------------------*
       3000-FINALIZAR                                          SECTION.
      *---------------------------------------------------------------*
            CLOSE ARQVSAM.
            PERFORM 1400-TESTAR-CLOSE.

       3000-99-FIM.                      EXIT.

      *---------------------------------------------------------------*
       9000-TRATARERROS                                        SECTION.
      *---------------------------------------------------------------*
             DISPLAY WRK-MSG-ERROPRINC.
             DISPLAY WRK-MSG-ERROPROGR     WRK-PROGRAMA.
             DISPLAY WRK-MSG-ERROSECAO     WRK-SECAO.
             DISPLAY WRK-MSG-ERROMSG       WRK-MSGERRO.
             DISPLAY WRK-MSG-ERROSTATUS    WRK-STATUS.
             GOBACK.
