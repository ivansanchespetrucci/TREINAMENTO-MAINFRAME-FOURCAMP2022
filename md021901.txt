      *=============================================================*   00001000
       IDENTIFICATION                            DIVISION.              00002000
      *=============================================================*   00003000
                                                                        00004000
       PROGRAM-ID. MD021901.                                            00005038
                                                                        00006000
      *=============================================================*   00007000
      *   AUTOR....:RAFAEL VIANA                                    *   00007100
      *   ANALISTA.:IVAN PETRUCCI                  - INSTRUTOR      *   00007200
      *   DATA ....:06/07/2022                                      *   00007336
      *-------------------------------------------------------------*   00007400
      * VERSAO   DATA       AUTOR     DESCRICAO DA MODIFICACAO      *   00007537
      *  1.0   06/07/22  RAFAEL VIANA     VERSAO INICIAL            *   00007637
      *-------------------------------------------------------------*   00007737
      *   OBJETIVO:FAZER A LEITURA DO ARQUIVO (EVSA0407), FORMATAR  *   00007836
      *          OS DADOS EM UM ARQUIVO SEQUENCIAL COM REGISTROS    *   00007900
      *          ONDE SALDO FOR MAIOR QUE 0 E SOMENTE NUMERICOS,    *   00008036
      *         SALDOS INCONSISTENTES SAO DESPREZADOS NA SYSOUT.    *   00008136
      *-------------------------------------------------------------*   00008200
RAFALT*                  A L T E R A C O E S                        *   00008352
RAFALT*-------------------------------------------------------------*   00008452
RAFALT* ANALISTA......: RAFAEL VIANA                                *   00008552
RAFALT* DATA..........: 06/07/2022                                  *   00008652
RAFALT*-------------------------------------------------------------*   00008752
RAFALT* OBJETIVO......:   GRAVAR DADOS INCONSISTENTES NO ARQUIVO    *   00008852
RAFALT*                              (SV0407EX) -LRECL 27-          *   00008961
RAFALT*-------------------------------------------------------------*   00009052
      *   ARQUIVOS:                                                 *   00009252
      *    DDNAME              I/O                  COPY/BOOK       *   00009352
      *    EVSA0407             I                    I#EV0407       *   00009452
      *    SVSA0407             O                    I#BV0407       *   00009552
      *-------------------------------------------------------------*   00009652
      *=============================================================*   00009752
                                                                        00009852
      *=============================================================*   00009952
       ENVIRONMENT                               DIVISION.              00010052
      *=============================================================*   00010152
                                                                        00010252
      *=============================================================*   00010352
       CONFIGURATION                               SECTION.             00010452
      *=============================================================*   00010552
       SPECIAL-NAMES.                                                   00010652
           DECIMAL-POINT IS COMMA.                                      00010752
      *-------------------------------------------------------------*   00010852
       INPUT-OUTPUT                                 SECTION.            00010952
       FILE-CONTROL.                                                    00011052
           SELECT EVSA0407 ASSIGN TO EVSA0407                           00011152
              ORGANIZATION IS INDEXED                                   00011252
              ACCESS MODE  IS SEQUENTIAL                                00011352
              RECORD KEY   IS ARQ-CHAVE                                 00011452
              FILE STATUS  IS FS-EVSA0407.                              00011552
                                                                        00011652
           SELECT SVSA0407 ASSIGN TO SVSA0407                           00011752
              FILE STATUS  IS FS-SVSA0407.                              00011852
                                                                        00011952
RAFALT     SELECT SV0407EX ASSIGN TO SV0407EX                           00012052
RAFALT        FILE STATUS  IS FS-SV0407EX.                              00012152
                                                                        00012252
      *=============================================================*   00012352
       DATA                                      DIVISION.              00012452
      *=============================================================*   00012552
       FILE                                        SECTION.             00012652
       FD EVSA0407.                                                     00012752
                                                                        00012852
       COPY 'I#EV0407'.                                                 00012952
                                                                        00013025
      *-------------------------------------------------------------*   00013103
                                                                        00013203
       FD SVSA0407                                                      00013311
           RECORDING MODE IS F                                          00013408
           BLOCK CONTAINS 0 RECORDS.                                    00013508
                                                                        00013603
       01 FD-SVSA0407-SAIDA   PIC X(27).                                00013854
                                                                        00013903
RAFALT*------------SAIDA DE ARQUIVOS COM INCONSISTENCIAS------------*   00014052
RAFALT FD SV0407EX                                                      00014151
RAFALT     RECORDING MODE IS F                                          00014251
RAFALT     BLOCK CONTAINS 0 RECORDS.                                    00014351
                                                                        00014451
RAFALT 01 FD-SV0407EX-SAIDA   PIC X(27).                                00014551
                                                                        00014651
      *=============================================================*   00014700
       WORKING-STORAGE                             SECTION.             00014800
      *=============================================================*   00014900
      *-------------------------------------------------------------*   00015049
       01 FILLER          PIC X(70) VALUE                               00015103
           '---------BOOK-ARQUIVO-SVSA0407-----------------'.           00015204
      *-------------------------------------------------------------*   00015349
                                                                        00015403
       COPY 'I#BV0407'.                                                 00015554
RAFALT 01 WRK-CORPO1    PIC X(27).                                      00020362
RAFALT*      10 WRK-SV0407EX-AGENCIA    PIC X(04).                      00020461
RAFALT*      10 WRK-SV0407EX-CONTA      PIC X(05).                      00020561
RAFALT*      10 WRK-SV0407EX-SALARIO    PIC X(10).                      00020661
RAFALT*      10 WRK-SV0407EX-DATA       PIC X(08).                      00020761
      *-------------------------------------------------------------*   00020855
       01 FILLER          PIC X(70) VALUE                               00020955
           '---------VARIAVEIS DE STATUS--------------------'.          00021055
      *-------------------------------------------------------------*   00021155
                                                                        00021255
       77 FS-EVSA0407      PIC 9(02).                                   00021355
       77 FS-SVSA0407      PIC 9(02).                                   00021455
RAFALT 77 FS-SV0407EX      PIC 9(02).                                   00021555
                                                                        00021655
      *-------------------------------------------------------------*   00021755
       01 FILLER          PIC X(70) VALUE                               00021855
           '---------RECEBER DATA E HORA--------------------'.          00021955
      *-------------------------------------------------------------*   00022055
                                                                        00022155
       01 WRK-DATAEHORA.                                                00022255
           05 WRK-DATA.                                                 00022355
              10 ANO                  PIC 9(04).                        00022455
              10 MES                  PIC 9(02).                        00022555
              10 DIA                  PIC 9(02).                        00022655
           05 WRK-HORA.                                                 00022755
              10 HORA                 PIC 9(02).                        00022855
              10 MINUTO               PIC 9(02).                        00022955
              10 SEGUNDO              PIC 9(02).                        00023055
                                                                        00023155
      *-------------------------------------------------------------*   00023255
       01 FILLER          PIC X(70) VALUE                               00023355
           '---------MENSAGENS DE ERRO----------------------'.          00023455
      *-------------------------------------------------------------*   00023555
                                                                        00023655
       77 WRK-MSGERRO      PIC X(30).                                   00023755
       77 WRK-CODIGOERRO   PIC X(30).                                   00023855
       77 WRK-MSGCODIGO    PIC X(12) VALUE 'CODIGO VSAM:'.              00023955
       77 WRK-MSGOPEN      PIC X(21) VALUE 'ERRO NO OPEN EVSA0407'.     00024055
       77 WRK-MSGOPENS     PIC X(21) VALUE 'ERRO NO OPEN SVSA0407'.     00024155
RAFALT 77 WRK-MSGOPENX     PIC X(21) VALUE 'ERRO NO OPEN SV0407EX'.     00024255
       77 WRK-MSGCLOSE     PIC X(22) VALUE 'ERRO NO CLOSE EVSA0407'.    00024355
       77 WRK-MSGCLOSES    PIC X(22) VALUE 'ERRO NO CLOSE SVSA0407'.    00024455
RAFALT 77 WRK-MSGCLOSEX    PIC X(22) VALUE 'ERRO NO CLOSE SV0407EX'.    00024555
       77 WRK-MSGVAZIO     PIC X(22) VALUE 'ARQUIVO EVSA0407 VAZIO'.    00024655
                                                                        00024755
      *-------------------------------------------------------------*   00024855
       01 FILLER          PIC X(70) VALUE                               00024955
           '---------MENSAGENS DO SISTEMA-------------------'.          00025055
      *-------------------------------------------------------------*   00025155
                                                                        00025255
       77 WRK-MSGREGLIDO   PIC X(33) VALUE                              00025355
                                   'TOTAL DE REGISTROS LIDOS.......:'.  00025455
                                                                        00025555
       77 WRK-MSGREGGRAV   PIC X(33) VALUE                              00025655
                                   'TOTAL DE REGISTROS GRAVADOS....:'.  00025755
                                                                        00025855
       77 WRK-MSGREGEXCE   PIC X(33) VALUE                              00025955
                                   'TOTAL DE REGISTROS DESPREZADO..:'.  00026055
                                                                        00026155
       77 WRK-MSGEXCECAO   PIC X(37) VALUE                              00026255
                                 '*------REGISTROS COM EXCECAO!------*'.00026355
                                                                        00026455
       77 WRK-MSGDATAHORA  PIC X(37) VALUE                              00026555
                                 '------------DATA E HORA------------:'.00026655
                                                                        00026755
       77 WRK-MSGSEPARA    PIC X(37) VALUE ALL '-'.                     00026855
                                                                        00026955
       77 WRK-MSGDATA      PIC X(10) VALUE '     DATA:'.                00027055
                                                                        00027155
       77 WRK-MSGHORA      PIC X(06) VALUE ' HORA:'.                    00027255
                                                                        00027355
      *-------------------------------------------------------------*   00027455
       01 FILLER          PIC X(70) VALUE                               00027555
           '---------ACUMULADAS-----------------------------'.          00027655
      *-------------------------------------------------------------*   00027755
                                                                        00027855
       77 WRK-REGLIDOS     PIC 9(03).                                   00027955
       77 WRK-REGGRAVADOS  PIC 9(03).                                   00028055
       77 WRK-EXCECAO      PIC 9(03).                                   00028155
                                                                        00028255
      *=============================================================*   00028355
       PROCEDURE DIVISION.                                              00028455
      *=============================================================*   00028555
                                                                        00028655
      *-------------------------------------------------------------*   00028755
       0000-PRINCIPAL                           SECTION.                00028855
      *-------------------------------------------------------------*   00028955
                                                                        00029055
            PERFORM  1000-INICIAR.                                      00029155
            PERFORM  1300-TESTAR-VAZIO.                                 00029255
            PERFORM  2000-PROCESSAR UNTIL FS-EVSA0407 EQUAL 10.         00029355
            PERFORM  3000-FINALIZAR.                                    00029455
            GOBACK.                                                     00029555
                                                                        00029655
                                                                        00029755
      *-------------------------------------------------------------*   00029855
       1000-INICIAR                             SECTION.                00029955
      *-------------------------------------------------------------*   00030055
            OPEN INPUT EVSA0407.                                        00030155
            OPEN OUTPUT SVSA0407.                                       00030255
RAFALT      OPEN OUTPUT SV0407EX.                                       00030359
            PERFORM 1100-TESTAR-STATUS.                                 00030455
            DISPLAY WRK-MSGEXCECAO.                                     00030555
            PERFORM 1200-RECEBE-DATA.                                   00030655
                                                                        00030755
       1000-99-FIM.              EXIT.                                  00030855
      *-------------------------------------------------------------*   00030955
       1100-TESTAR-STATUS                       SECTION.                00031055
      *-------------------------------------------------------------*   00031155
            IF FS-EVSA0407 NOT EQUAL ZEROS                              00031255
              MOVE WRK-MSGOPEN TO WRK-MSGERRO                           00031355
              MOVE FS-EVSA0407 TO WRK-CODIGOERRO                        00031455
              PERFORM 9000-TRATAERROS                                   00031555
            END-IF.                                                     00031655
                                                                        00031755
            IF FS-SVSA0407 NOT EQUAL ZEROS                              00031855
              MOVE WRK-MSGOPENS TO WRK-MSGERRO                          00031955
              MOVE FS-SVSA0407  TO WRK-CODIGOERRO                       00032055
              PERFORM 9000-TRATAERROS                                   00032155
            END-IF.                                                     00032255
                                                                        00032355
RAFALT      IF FS-SVSA0407 NOT EQUAL ZEROS                              00032455
RAFALT        MOVE WRK-MSGOPENS TO WRK-MSGERRO                          00032555
RAFALT        MOVE FS-SVSA0407  TO WRK-CODIGOERRO                       00032655
RAFALT        PERFORM 9000-TRATAERROS                                   00032755
RAFALT      END-IF.                                                     00032855
       1100-99-FIM.              EXIT.                                  00032955
                                                                        00033055
      *-------------------------------------------------------------*   00033155
       1200-RECEBE-DATA                         SECTION.                00033255
      *-------------------------------------------------------------*   00033355
              MOVE FUNCTION CURRENT-DATE(1:14) TO WRK-DATAEHORA.        00033455
              ACCEPT WRK-SVSA0407-DADOS-DATA FROM DATE YYYYMMDD.        00033555
       1200-99-FIM.              EXIT.                                  00033755
                                                                        00033855
      *-------------------------------------------------------------*   00033955
       1300-TESTAR-VAZIO                        SECTION.                00034055
      *-------------------------------------------------------------*   00034155
            READ EVSA0407.                                              00034255
            IF FS-EVSA0407 EQUAL 10                                     00034355
              MOVE WRK-MSGVAZIO TO WRK-MSGERRO                          00034455
              MOVE FS-EVSA0407  TO WRK-CODIGOERRO                       00034555
            END-IF.                                                     00034655
                                                                        00034755
       1300-99-FIM.              EXIT.                                  00034855
      *-------------------------------------------------------------*   00034955
       1400-TESTAR-CLOSE                        SECTION.                00035055
      *-------------------------------------------------------------*   00035155
            IF FS-EVSA0407 NOT EQUAL ZEROS                              00035255
              MOVE WRK-MSGCLOSE  TO WRK-MSGERRO                         00035355
              MOVE FS-EVSA0407   TO WRK-CODIGOERRO                      00035455
              PERFORM 9000-TRATAERROS                                   00035555
            END-IF.                                                     00035655
                                                                        00035755
            IF FS-SVSA0407 NOT EQUAL ZEROS                              00035855
              MOVE WRK-MSGCLOSES TO WRK-MSGERRO                         00035955
              MOVE FS-SVSA0407   TO WRK-CODIGOERRO                      00036055
              PERFORM 9000-TRATAERROS                                   00036155
            END-IF.                                                     00036255
                                                                        00036355
RAFALT      IF FS-SV0407EX NOT EQUAL ZEROS                              00036455
RAFALT        MOVE WRK-MSGCLOSEX TO WRK-MSGERRO                         00036555
RAFALT        MOVE FS-SV0407EX   TO WRK-CODIGOERRO                      00036655
RAFALT        PERFORM 9000-TRATAERROS                                   00036755
RAFALT      END-IF.                                                     00036855
                                                                        00036955
       1400-99-FIM.              EXIT.                                  00037055
      *-------------------------------------------------------------*   00037155
       2000-PROCESSAR                           SECTION.                00037255
      *-------------------------------------------------------------*   00037355
            IF ARQ-SALARIO EQUAL ZEROS OR ARQ-SALARIO IS NOT NUMERIC    00037455
RAFALT*       MOVE ARQ-AGENCIA  TO WRK-SV0407EX-AGENCIA                 00037561
RAFALT*       MOVE ARQ-CONTA    TO WRK-SV0407EX-CONTA                   00037661
RAFALT*       MOVE ARQ-SALARIO  TO WRK-SV0407EX-SALARIO                 00037761
RAFALT        MOVE REG-EVSA0407 TO WRK-CORPO1                           00037861
      *       MOVE ARQ-AGENCIA  TO WRK-SVSA0407-DADOS-AGENCIA           00037961
      *       MOVE ARQ-CONTA    TO WRK-SVSA0407-DADOS-CONTA             00038061
      *       MOVE ARQ-SALARIO  TO WRK-SVSA0407-DADOS-SALARIO           00038161
      *        DISPLAY WRK-CORPO                                        00038261
RAFALT         DISPLAY WRK-CORPO1                                       00038362
RAFALT         WRITE FD-SV0407EX-SAIDA FROM WRK-CORPO1                  00038461
               ADD 1 TO WRK-EXCECAO                                     00038561
            ELSE                                                        00038661
              MOVE ARQ-AGENCIA  TO WRK-SVSA0407-DADOS-AGENCIA           00038761
              MOVE ARQ-CONTA    TO WRK-SVSA0407-DADOS-CONTA             00038861
              MOVE ARQ-SALARIO  TO WRK-SVSA0407-DADOS-SALARIO           00038961
               WRITE FD-SVSA0407-SAIDA FROM WRK-CORPO                   00039061
               ADD 1 TO WRK-REGGRAVADOS                                 00039161
            END-IF.                                                     00039261
            ADD 1 TO WRK-REGLIDOS.                                      00039361
            READ EVSA0407.                                              00039461
                                                                        00039561
       2000-99-FIM.              EXIT.                                  00039661
      *-------------------------------------------------------------*   00039761
       3000-FINALIZAR                               SECTION.            00039861
      *-------------------------------------------------------------*   00039961
            DISPLAY WRK-MSGSEPARA.                                      00040061
              CLOSE EVSA0407.                                           00040161
              CLOSE SVSA0407.                                           00040261
RAFALT        CLOSE SV0407EX.                                           00040361
            PERFORM 1400-TESTAR-CLOSE.                                  00040461
                                                                        00040561
             DISPLAY WRK-MSGREGLIDO WRK-REGLIDOS.                       00040661
             DISPLAY WRK-MSGREGGRAV WRK-REGGRAVADOS.                    00040761
             DISPLAY WRK-MSGREGEXCE WRK-EXCECAO.                        00040861
             DISPLAY WRK-MSGSEPARA.                                     00040961
             DISPLAY WRK-MSGDATAHORA.                                   00041061
             DISPLAY WRK-MSGDATA WRK-DATA WRK-MSGHORA WRK-HORA.         00041161
                                                                        00041261
       3000-99-FIM.              EXIT.                                  00042055
      *-------------------------------------------------------------*   00047100
       9000-TRATAERROS                              SECTION.            00047200
      *-------------------------------------------------------------*   00047300
            DISPLAY WRK-MSGERRO.                                        00047404
            DISPLAY WRK-MSGCODIGO WRK-CODIGOERRO.                       00047504
            GOBACK.                                                     00047602
                                                                        00047700
       9000-99-FIM.              EXIT.                                  00047800
