      *=============================================================*   00001000
       IDENTIFICATION                            DIVISION.              00002000
      *=============================================================*   00003000
                                                                        00004000
       PROGRAM-ID. MD021902.                                            00005000
                                                                        00006000
      *=============================================================*   00007000
      *   AUTOR....:RAFAEL VIANA                                    *   00007100
      *   ANALISTA.:IVAN PETRUCCI                  - INSTRUTOR      *   00007200
      *   DATA ....:06/07/2022                                      *   00007300
      *-------------------------------------------------------------*   00007400
      * VERSAO   DATA       AUTOR     DESCRICAO DA MODIFICACAO      *   00007500
      *  1.0   06/07/22  RAFAEL VIANA     VERSAO INICIAL            *   00007600
      *-------------------------------------------------------------*   00007700
      *   OBJETIVO:FAZER A LEITURA E GRAVACAO DE UM REGISTRO        *   00007800
      *            UTILIZANDO (PARM) NA EXECUCAO.                   *   00007900
      *                                                             *   00008000
      *                                                             *   00008100
      *-------------------------------------------------------------*   00008200
      *                  A L T E R A C O E S                        *   00008353
RAFALT*-------------------------------------------------------------*   00008454
  .   * ANALISTA......:RAFAEL VIANA                     4SYS        *   00008555
  .   * DATA..........:07/07/2020                                   *   00008655
  .   *-------------------------------------------------------------*   00008855
  .   * OBJETIVO......:IMPLEMENTACAO DE EXCLUSAO E REGRAVACAO.      *   00008955
RAFALT*                 CODIGO PARM:      (E)         (R)           *   00009054
      *-------------------------------------------------------------*   00009153
      *                  A L T E R A C O E S                        *   00009253
RAFAL2*-------------------------------------------------------------*   00009354
  .   * ANALISTA......:RAFAEL VIANA                                 *   00009455
  .   * DATA..........:07/07/2020                                   *   00009555
  .   *-------------------------------------------------------------*   00009655
  .   * OBJETIVO......:IMPLEMENTACAO DE CONSISTENCIA NOS DADOS      *   00009755
  .   *                                                             *   00009855
RAFAL2*-------------------------------------------------------------*   00009954
      *   ARQUIVOS:                                                 *   00010053
      *    DDNAME              I/O                  COPY/BOOK       *   00010153
      *    EVSA0407            I-O                   I#EV0407       *   00010253
      *-------------------------------------------------------------*   00010353
      *=============================================================*   00010453
                                                                        00010553
      *=============================================================*   00010653
       ENVIRONMENT                               DIVISION.              00010753
      *=============================================================*   00010853
                                                                        00010953
      *=============================================================*   00011053
       CONFIGURATION                               SECTION.             00011153
      *=============================================================*   00011253
       SPECIAL-NAMES.                                                   00011353
           DECIMAL-POINT IS COMMA.                                      00011453
      *-------------------------------------------------------------*   00011553
       INPUT-OUTPUT                                 SECTION.            00011653
       FILE-CONTROL.                                                    00011753
           SELECT EVSA0407 ASSIGN TO EVSA0407                           00011853
              ORGANIZATION IS INDEXED                                   00011953
              ACCESS MODE  IS DYNAMIC                                   00012053
              RECORD KEY   IS ARQ-CHAVE                                 00012153
              FILE STATUS  IS FS-EVSA0407.                              00012253
                                                                        00012353
                                                                        00012453
      *=============================================================*   00012553
       DATA                                      DIVISION.              00012653
      *=============================================================*   00012753
       FILE                                        SECTION.             00012853
       FD EVSA0407.                                                     00012953
      *-----------LRCL 19-------------------------------------------*   00013060
       COPY 'I#EV0407'.                                                 00014053
                                                                        00014400
      *=============================================================*   00014500
       WORKING-STORAGE                             SECTION.             00014600
      *=============================================================*   00014700
      *-------------------------------------------------------------*   00015900
       01 FILLER          PIC X(70) VALUE                               00016000
           '---------VARIAVEIS DE STATUS--------------------'.          00017000
      *-------------------------------------------------------------*   00018000
                                                                        00019000
       77 FS-EVSA0407      PIC 9(02).                                   00020000
                                                                        00021200
      *-------------------------------------------------------------*   00022800
       01 FILLER          PIC X(70) VALUE                               00022900
           '---------MENSAGENS DE ERRO----------------------'.          00023000
      *-------------------------------------------------------------*   00023100
                                                                        00023200
       77 WRK-MSGERRO      PIC X(30).                                   00023300
       77 WRK-CODIGOERRO   PIC X(30).                                   00023400
       77 WRK-MSGCODIGO    PIC X(12) VALUE 'CODIGO VSAM:'.              00023500
       77 WRK-MSGOPEN      PIC X(21) VALUE 'ERRO NO OPEN EVSA0407'.     00023600
       77 WRK-MSGCLOSE     PIC X(22) VALUE 'ERRO NO CLOSE EVSA0407'.    00023900
       77 WRK-MSGVAZIO     PIC X(22) VALUE 'ARQUIVO EVSA0407 VAZIO'.    00024200
       77 WRK-MSGSEPARA    PIC X(37) VALUE ALL '-'.                     00024308
       77 WRK-MSGERROLEN   PIC X(26) VALUE                              00024426
                                 'TAMANHO DA CHAVE INCORRETA'.          00024526
                                                                        00024626
                                                                        00024700
      *-------------------------------------------------------------*   00024800
       01 FILLER          PIC X(70) VALUE                               00024900
           '---------MENSAGENS DO SISTEMA-------------------'.          00025000
      *-------------------------------------------------------------*   00025100
                                                                        00026000
                                                                        00026300
       77 WRK-MSGNENCONTR  PIC X(23) VALUE                              00026405
                                 'REGISTRO NAO ENCONTRADO'.             00026505
                                                                        00026605
       77 WRK-MSGNGRAVADO  PIC X(23) VALUE                              00026705
                                 'REGISTRO NAO GRAVADO...'.             00026805
                                                                        00026913
       77 WRK-MSGEXCLUIDO  PIC X(23) VALUE                              00027313
                                 'REGISTRO EXCLUIDO......'.             00027413
                                                                        00027513
       77 WRK-MSGREGGRAV   PIC X(23) VALUE                              00027628
                                 'REGISTRO GRAVADO.......'.             00027728
                                                                        00027828
       77 WRK-MSGREGREGR   PIC X(23) VALUE                              00027928
                                 'REGISTRO REGRAVADO.....'.             00028028
                                                                        00028128
                                                                        00028529
      *-------------------------------------------------------------*   00028629
       01 FILLER          PIC X(70) VALUE                               00028729
           '---------MASCARA E APOIO------------------------'.          00028829
      *-------------------------------------------------------------*   00028929
                                                                        00029029
       77 WRK-LEN-ED      PIC Z99.                                      00029129
                                                                        00029229
      *=============================================================*   00029329
       LINKAGE                                  SECTION.                00029429
      *=============================================================*   00029529
       01 LNK-ENTRADA.                                                  00029629
          05 LNK-LEN            PIC 9(04) COMP.                         00029729
          05 LNK-CORPO.                                                 00029829
             10 LNK-OPER        PIC X(01).                              00029929
             10 LNK-CHAVE.                                              00030029
                15 LNK-AGENCIA  PIC X(04).                              00030129
                15 LNK-CONTA    PIC X(05).                              00030229
             10 LNK-SALARIO     PIC X(10).                              00030329
                                                                        00030429
      *=============================================================*   00030529
       PROCEDURE DIVISION       USING LNK-ENTRADA.                      00030629
      *=============================================================*   00030729
                                                                        00030829
      *-------------------------------------------------------------*   00030929
       0000-PRINCIPAL                           SECTION.                00031029
      *-------------------------------------------------------------*   00031129
                                                                        00031229
            PERFORM  1000-INICIAR.                                      00031329
             EVALUATE LNK-OPER                                          00031629
               WHEN 'L'                                                 00031729
                PERFORM  2000-LEITURA                                   00031829
               WHEN 'G'                                                 00031929
                PERFORM  2100-GRAVACAO                                  00032029
RAFALT         WHEN 'E'                                                 00032129
  .             PERFORM  2200-EXCLUSAO                                  00032255
  .            WHEN 'R'                                                 00032355
RAFALT          PERFORM  2300-REGRAVAR                                  00032429
             END-EVALUATE.                                              00032529
            PERFORM  3000-FINALIZAR.                                    00032629
            GOBACK.                                                     00032729
                                                                        00032829
                                                                        00032929
      *-------------------------------------------------------------*   00033029
       1000-INICIAR                             SECTION.                00033129
      *-------------------------------------------------------------*   00033229
            OPEN I-O EVSA0407.                                          00033329
            PERFORM 1100-TESTAR-STATUS.                                 00033429
                                                                        00033529
       1000-99-FIM.              EXIT.                                  00033629
      *-------------------------------------------------------------*   00033729
       1100-TESTAR-STATUS                       SECTION.                00033829
      *-------------------------------------------------------------*   00033929
            IF FS-EVSA0407 NOT EQUAL ZEROS                              00034029
              MOVE WRK-MSGOPEN TO WRK-MSGERRO                           00034129
              MOVE FS-EVSA0407 TO WRK-CODIGOERRO                        00034229
              PERFORM 9100-TRATAERROS-SAIDA                             00034334
            END-IF.                                                     00034429
                                                                        00034529
       1100-99-FIM.              EXIT.                                  00034629
                                                                        00034729
      *-------------------------------------------------------------*   00035929
       1400-TESTAR-CLOSE                        SECTION.                00036029
      *-------------------------------------------------------------*   00036129
            IF FS-EVSA0407 NOT EQUAL ZEROS                              00036229
              MOVE WRK-MSGCLOSE  TO WRK-MSGERRO                         00036329
              MOVE FS-EVSA0407   TO WRK-CODIGOERRO                      00036429
              PERFORM 9000-TRATAERROS                                   00036529
            END-IF.                                                     00036629
                                                                        00036729
                                                                        00037600
       1400-99-FIM.              EXIT.                                  00037700
      *-------------------------------------------------------------*   00037800
       2000-LEITURA                             SECTION.                00037900
      *-------------------------------------------------------------*   00038000
RAFAL2       IF LNK-LEN NOT EQUAL 10                                    00038225
  .              MOVE WRK-MSGERROLEN TO WRK-MSGERRO                     00038355
  .              MOVE FS-EVSA0407 TO WRK-CODIGOERRO                     00038455
  .              PERFORM 9000-TRATAERROS                                00038555
RAFAL2       END-IF.                                                    00038627
                                                                        00039018
             MOVE LNK-AGENCIA TO ARQ-AGENCIA.                           00039100
             MOVE LNK-CONTA   TO ARQ-CONTA.                             00040000
             READ EVSA0407.                                             00040100
              IF FS-EVSA0407 EQUAL ZEROS                                00040200
                 DISPLAY WRK-MSGSEPARA                                  00040306
                 DISPLAY REG-EVSA0407                                   00040400
              ELSE                                                      00040500
                 MOVE WRK-MSGNENCONTR TO WRK-MSGERRO                    00040605
                 MOVE FS-EVSA0407 TO WRK-CODIGOERRO                     00040715
                 PERFORM 9000-TRATAERROS                                00040800
              END-IF.                                                   00040900
                                                                        00041000
       2000-99-FIM.              EXIT.                                  00041100
      *-------------------------------------------------------------*   00041200
       2100-GRAVACAO                            SECTION.                00041300
      *-------------------------------------------------------------*   00041400
RAFAL2       IF LNK-LEN NOT EQUAL 20                                    00041526
  .              MOVE WRK-MSGERROLEN TO WRK-MSGERRO                     00041655
  .              MOVE FS-EVSA0407 TO WRK-CODIGOERRO                     00041755
  .              PERFORM 9000-TRATAERROS                                00041855
  .          END-IF.                                                    00041955
  .          MOVE LNK-AGENCIA TO ARQ-AGENCIA.                           00042355
  .          MOVE LNK-CONTA   TO ARQ-CONTA.                             00042455
  .          IF LNK-SALARIO IS NOT NUMERIC                              00042555
  .              MOVE WRK-MSGERROLEN TO WRK-MSGERRO                     00042655
  .              MOVE FS-EVSA0407 TO WRK-CODIGOERRO                     00042755
  .              PERFORM 9000-TRATAERROS                                00042855
RAFAL2       END-IF.                                                    00042926
             MOVE LNK-SALARIO TO ARQ-SALARIO.                           00043000
             WRITE REG-EVSA0407                                         00043100
                IF FS-EVSA0407 EQUAL ZEROS                              00043200
                   DISPLAY REG-EVSA0407                                 00043300
                   DISPLAY WRK-MSGSEPARA                                00043428
                   DISPLAY WRK-MSGREGGRAV                               00043528
                ELSE                                                    00043600
                   MOVE WRK-MSGNGRAVADO TO WRK-MSGERRO                  00043705
                   MOVE FS-EVSA0407 TO WRK-CODIGOERRO                   00043815
                   PERFORM 9000-TRATAERROS                              00043900
                END-IF.                                                 00044000
                                                                        00044100
       2100-99-FIM.              EXIT.                                  00044200
RAFALT*-------------------------------------------------------------*   00044311
  .    2200-EXCLUSAO                            SECTION.                00044455
RAFALT*-------------------------------------------------------------*   00044511
                                                                        00044627
RAFAL2       IF LNK-LEN NOT EQUAL 10                                    00044726
  .              MOVE WRK-MSGERROLEN TO WRK-MSGERRO                     00044855
  .              MOVE FS-EVSA0407 TO WRK-CODIGOERRO                     00044955
  .              PERFORM 9000-TRATAERROS                                00045055
  .             GO TO 2200-99-FIM                                       00045155
RAFAL2       END-IF.                                                    00045244
                                                                        00045327
RAFALT         MOVE LNK-CHAVE   TO ARQ-CHAVE                            00045442
  .            READ EVSA0407                                            00045555
  .           IF FS-EVSA0407 EQUAL ZEROS                                00045755
  .             DELETE EVSA0407                                         00045855
  .             DISPLAY WRK-MSGSEPARA                                   00045955
  .             DISPLAY WRK-MSGEXCLUIDO                                 00046055
  .           ELSE                                                      00046255
  .             MOVE WRK-MSGNENCONTR TO WRK-MSGERRO                     00046355
  .             MOVE FS-EVSA0407 TO WRK-CODIGOERRO                      00046455
  .             PERFORM 9000-TRATAERROS                                 00046555
  .           END-IF.                                                   00046755
  .    2200-99-FIM.              EXIT.                                  00046855
  .   *-------------------------------------------------------------*   00046955
  .    2300-REGRAVAR                            SECTION.                00047055
RAFALT*-------------------------------------------------------------*   00047111
                                                                        00047227
RAFAL2       IF LNK-LEN NOT EQUAL 20                                    00047326
  .              MOVE WRK-MSGERROLEN TO WRK-MSGERRO                     00047455
  .              MOVE FS-EVSA0407 TO WRK-CODIGOERRO                     00047555
  .              PERFORM 9000-TRATAERROS                                00047655
  .              GO TO 2300-99-FIM                                      00047755
RAFAL2       END-IF.                                                    00047827
                                                                        00047927
RAFALT       MOVE LNK-AGENCIA TO ARQ-AGENCIA.                           00048112
  .          MOVE LNK-CONTA   TO ARQ-CONTA.                             00048255
  .            READ EVSA0407.                                           00048455
  .             IF FS-EVSA0407 EQUAL ZEROS                              00048555
  .              MOVE LNK-SALARIO TO ARQ-SALARIO                        00048657
  .               REWRITE REG-EVSA0407                                  00048755
  .                DISPLAY REG-EVSA0407                                 00048855
  .                DISPLAY WRK-MSGSEPARA                                00048955
  .                DISPLAY WRK-MSGREGREGR                               00049055
  .              ELSE                                                   00049155
  .                MOVE WRK-MSGNGRAVADO TO WRK-MSGERRO                  00049255
  .                MOVE FS-EVSA0407 TO WRK-CODIGOERRO                   00049355
  .                PERFORM 9000-TRATAERROS                              00049455
  .             END-IF.                                                 00049555
RAFALT 2300-99-FIM.              EXIT.                                  00049611
      *-------------------------------------------------------------*   00049711
       3000-FINALIZAR                               SECTION.            00049811
      *-------------------------------------------------------------*   00049911
              DISPLAY WRK-MSGSEPARA.                                    00050011
              CLOSE EVSA0407.                                           00050111
              PERFORM 1400-TESTAR-CLOSE.                                00050211
                                                                        00050311
                                                                        00050411
       3000-99-FIM.              EXIT.                                  00050511
      *-------------------------------------------------------------*   00050611
       9000-TRATAERROS                              SECTION.            00050711
      *-------------------------------------------------------------*   00050811
            DISPLAY WRK-MSGERRO.                                        00050911
            DISPLAY WRK-MSGCODIGO WRK-CODIGOERRO.                       00051011
                                                                        00052011
       9000-99-FIM.              EXIT.                                  00060011
      *-------------------------------------------------------------*   00070034
       9100-TRATAERROS-SAIDA                        SECTION.            00080034
      *-------------------------------------------------------------*   00090034
            DISPLAY WRK-MSGERRO.                                        00100034
            DISPLAY WRK-MSGCODIGO WRK-CODIGOERRO.                       00110034
            DISPLAY WRK-MSGSEPARA.                                      00120034
            GOBACK.                                                     00130034
                                                                        00140034
       9000-99-FIM.              EXIT.                                  00150034
